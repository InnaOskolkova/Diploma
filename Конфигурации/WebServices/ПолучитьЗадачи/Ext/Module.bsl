Функция Get()
	
	Попытка 
		
		НазваниеОбъекта = "Операции по задачам";
		МетаWebСервис = Метаданные.WebСервисы.ПолучитьЗадачи;
		
		///// Запись в журнал регистрации сведений о параметрах запроса к справочникам
		СведенияОЗапросе = "Запрос данных по документу """ + НазваниеОбъекта + """";
		ЗаписьЖурналаРегистрации(СведенияОЗапросе,
		УровеньЖурналаРегистрации.Информация,
		МетаWebСервис,
		,
		СведенияОЗапросе);
		///////////////////////////////////////////////////////////////////////////////
		
		Запрос = Новый Запрос;
		
		
		Запрос.Текст =  "ВЫБРАТЬ
		                |	ДействиеПоЗадаче.Задача.Код КАК NUMBER,
		                |	ДействиеПоЗадаче.Задача.Наименование КАК NAME,
		                |	ДействиеПоЗадаче.Статус КАК STATUS,
		                |	ДействиеПоЗадаче.Инициатор КАК CUSTOMER,
		                |	ДействиеПоЗадаче.Исполнитель КАК PERFORMER,
		                |	ДействиеПоЗадаче.ПлановоеОкончаниеРаботПоСтатусу КАК FINAL
		                |ИЗ
		                |	Документ.ДействиеПоЗадаче КАК ДействиеПоЗадаче";
	 			   
		
		ТЗдляОбработки = Запрос.Выполнить().Выгрузить();
		КоличествоЗаписей = ТЗдляОбработки.Количество();
		
		ListТип = ФабрикаXDTO.Тип("http://gazprom-neft.ru/ksunsi/1cksu/UprZNI","List"); 		
		List =  ФабрикаXDTO.Создать(ListТип);
		
		ZNIТип = ФабрикаXDTO.Тип("http://gazprom-neft.ru/ksunsi/1cksu/UprZNI","ZNI"); 		
	  			
		Для Каждого СтрокаТЗ Из ТЗдляОбработки Цикл
			ZNI = ФабрикаXDTO.Создать(ZNIТип);
			ZNI.NUMBER 	= СтроковыеФункции.СериализоватьЗначениеВСтроку(СтрокаТЗ.NUMBER);
			ZNI.NAME 		= СтроковыеФункции.СериализоватьЗначениеВСтроку(СтрокаТЗ.NAME);
			ZNI.STATUS 	= СтроковыеФункции.СериализоватьЗначениеВСтроку(СтрокаТЗ.STATUS);
			ZNI.CUSTOMER	= СтроковыеФункции.СериализоватьЗначениеВСтроку(СтрокаТЗ.CUSTOMER);
			ZNI.PERFORMER	= СтроковыеФункции.СериализоватьЗначениеВСтроку(СтрокаТЗ.PERFORMER);
			ZNI.FINAL 		= СтроковыеФункции.СериализоватьЗначениеВСтроку(СтрокаТЗ.FINAL); 
			List.ZNI.Добавить(ZNI);
		КонецЦикла;	
		
		Response = ПолучитьXMLИзXDTO(List);
		
		///// Запись в журнал регистрации сведений о результате обработки
		СведенияОЗапросе = СтроковыеФункции.ПодставитьПараметрыВСтроку("Передано %1 элемент(ов) объекта """ + НазваниеОбъекта + """", КоличествоЗаписей);
		ЗаписьЖурналаРегистрации(СведенияОЗапросе,
		УровеньЖурналаРегистрации.Информация,
		МетаWebСервис,
		,
		СведенияОЗапросе);
		///////////////////////////////////////////////////////////////////////////////
		              		
	Исключение
		
		///// Запись в журнал регистрации сведений о результате обработки
		СведенияОЗапросе = "Ошибка при работе web-сервиса: " + ОписаниеОшибки();
		ЗаписьЖурналаРегистрации(СведенияОЗапросе,
		УровеньЖурналаРегистрации.Информация,
		МетаWebСервис,
		,
		СведенияОЗапросе);
		///////////////////////////////////////////////////////////////////////////////
		
		Возврат "ERROR";
		
	КонецПопытки;		
	
	Возврат Response;
	
КонецФункции

Функция ПолучитьXMLИзXDTO(XDTO)
		
	ЗаписьXML = Новый ЗаписьXML; 
	ЗаписьXML.УстановитьСтроку();
	ЗаписьXML.ЗаписатьОбъявлениеXML(); 
	ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, XDTO); 
	СтрокаXML = ЗаписьXML.Закрыть();
	
	Возврат СтрокаXML;
	
КонецФункции




